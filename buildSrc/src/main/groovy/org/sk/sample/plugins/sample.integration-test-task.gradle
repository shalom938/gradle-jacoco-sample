testing {
    suites {
        integrationTest(JvmTestSuite) {
            useJUnitJupiter(testinglibs.versions.junit.jupiter.get())
            dependencies {
                implementation project
                implementation project(':test-utils')
            }
            sources {
                java {
                    srcDir 'src/integrationTest/java'
                }
                resources {
                    srcDir 'src/integrationTest/resources'
                }
            }

            targets {
                all {
                    testTask.configure {
                        //flexible ordering, gradle may choose differently or
                        //run them in parallel. if the test and integration tests can not
                        //run at the same time then change to mustRunAfter.
                        //dependsOn is not suitable here because we want to be able to run only
                        //the integration test without dependency on test.
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

tasks.named('check') {
    dependsOn(testing.suites.integrationTest)
}
