plugins {
    id 'java-library'
}

dependencies {
    implementation project(':utilities')
    //journals-all is from an included build
    api 'org.sk.sample.journal:journals-all:1.0'
    implementation libs.google.guava
}

compileJava{
    //because this module has a qualified export 'exports org.sk.sample.messagejournal to sample.messenger'
    //and sample.messenger is not available during compile of this module the compiler emits a warning.
    //this will also disable other warnings related to modules.
    options.compilerArgs += ['-Xlint:-module']
}


testing {
    suites {
        test {
            useJUnitJupiter(testinglibs.versions.junit.jupiter.get())
        }
    }
}


test {
    testLogging {
        showStandardStreams project.hasProperty("show.unit.tests.stdout")
        events "passed", "skipped", "failed"
        showStackTraces true
        exceptionFormat 'full'
        showCauses true
    }
}